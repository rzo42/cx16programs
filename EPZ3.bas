1 REM "RESIZE SCREEN"

2REM "CREATES A 20X6 ARRAY FOR ZOMBIE DATA"
3REM "(ZOMBIE,ATTRIBUTES"
4 REM "0=X,1=Y,2=HP,3=ST,4=SP,5=ITEM,6=zombie/survivor,7=behavior variant"


5 DIMZA(30,8):DIMBE(9):BE(7)=127:BE(8)=-1
6 BE(0)=0:BE(1)=-129:BE(2)=-128:BE(3)=-127:BE(4)=1:BE(5)=129:BE(6)=128

7VPOKE$F,1,41:VPOKE $F,2,45


8REM"LOADS CHARACTER DATA"
9 VLOAD"ztxt.bin",8,0,$f800

10 REM "LOADS COLOR PALETTE"

11 VLOAD"zpal.bin",8,$F,$1000

12 REM "SETS BGC TO BLACK/CLEARS SCREEN"

13 COLOR0,0:PRINTCHR$(147)

14 REM "TITLE SCREEN"
15VLOAD"zt.bin",8,0,0
16 TZ=TI+2
17 IFTI<TZTHEN17
18 IFNOTJOY(1)=16GOTO16

19 REM "FLICKERS SCREEN"

20 FOR T=0TO5:VPOKE$F,$1001,255:VPOKE$F,$1000,255:TZ=TI+2
21 IFTI<TZTHEN21
22 VPOKE$F,$1000,0:VPOKE$F,$1001,0:TZ=TI+1
23 IFTI<TZTHEN23
24 NEXTT:GOTO25






25 REM "PRINTS MENU"

26 PRINT "{home}{down}{right}{yellow}exit plan z iii : zedco."
27 PRINT "{down}{right}                  "
28 PRINT "{right}{white}]childs play      "
29 PRINT "{right}                  "
30 PRINT "{right} mostly harmless  "
31 PRINT "{right}                  "
32 PRINT "{right} minor struggle   "
33 PRINT "{right}                  "
34 PRINT "{right} absurd           "
35 PRINT "{right}                  "
36 PRINT "{right} endless nightmare"
37 PRINT "{right}                  "
38 PRINT "{right} story            "
39 PRINT "{right}                  "
40 PRINT "{right} directions       "
41 PRINT "{right}                  "
42 PRINT "{right} symbol key       "
43 PRINT "{right}                  "

44 REM "menu controls"
45FORT=0TO15:VPOKE$F,$1010+T,VPEEK($F,$1020+T):NEXTT:Y=1:SC=257


46JS=JOY(1):X=INT(RND(1)*255)+0

47 IFJS=4THENY=Y+1:IFY>8THENY=8:GOTO50
48 IFJS=8THENY=Y-1:IFY<1THENY=1:GOTO50
49 IFJS=16GOTO56
50FORT=0TO100:X=INT(RND(1)*255)+0:NEXTT
51VPOKE0,(SC+(Y*256))*2,29:VPOKE0,((SC+(Y*256))*2)+1,8
52 IFY>1THENVPOKE0,(SC+((Y-1)*256))*2,32
53 IFY<8THENVPOKE0,(SC+((Y+1)*256))*2,32
54 GOTO46
55REM "MENU SELECT RELAY"

56 BR=0:RX=0:FL=0:SC=SC+128:L=1:BM=0:MN=0:SV=0:KY=0:VR=0:G1=0:T$="010000"
57 ON Y GOTO58,59,60,61,62,63,64,65

58 HP=4:PW=5:BT=10:GG=300:W=3:LM=5:Z1=1:GOTO67

59 HP=4:PW=4:BT=2:GG=200:W=5:LM=10:Z1=2:GOTO67

60 HP=3:PW=3:BT=1:GG=100:W=7:LM=15:Z1=3:GOTO67

61 HP=2:PW=2:BT=0:GG=0:W=5:LM=20:Z1=4:GOTO67

62 HP=3:PW=3:BT=3:GG=200:W=5:LM=21:Z1=4:GOTO67

63GOTO45
64GOTO45
65GOTO45

66REM "STAT SET"

67VPOKE0,(21+(7*128))*2,131+HP:VPOKE0,(24+(7*128))*2,131+PW
68VPOKE0,(21+(9*128))*2,131+BT:VPOKE0,(21+(13*128))*2,131+INT(GG/5)
69VPOKE0,(24+(17*128))*2,131+L:VPOKE0,(24+(4*128))*2,65

70 REM "SCREEN FLICKER"

71 FOR T=0TO5:VPOKE$F,$1001,255:VPOKE$F,$1000,255:TZ=TI+2
72 IFTI<TZTHEN72
73 VPOKE$F,$1000,0:VPOKE$F,$1001,0:TZ=TI+1
74 IFTI<TZTHEN74

75 NEXTT

76 REM"ROOM GENERATOR

77 PRINT"{home}{ORANGE}{down}{right}generating level        "

78 REM "CLEARS MAP"

79FORY=0TO16:FORX=0TO17:VPOKE0,(SC+X+(Y*128))*2,66
80 VPOKE0,((SC+X+(Y*128))*2)+1,0:NEXTX:NEXTY

81REM "GENERATES WALLS"
82 FORT=0TOL+W:X=INT(RND(1)*18)+0:Y=(INT(RND(1)*17)+0)*128
83 VPOKE0,(SC+X+Y)*2,68:VPOKE0,((SC+X+Y)*2)+1,$0A
84NEXTT

85 REM "ZOMBIE GENERATOR"
86 FORT=0TOZ1+L-1
87 X=INT(RND(1)*18)+0:Y=(INT(RND(1)*17)+0)
88 IFVPEEK(0,(SC+X+(Y*128))*2)>66THEN87
89 ZA(T,0)=X:ZA(T,1)=Y:ZA(T,2)=INT(RND(1)*(LV+Z1))+1
90 ZA(T,3)=INT(RND(1)*(LV+Z1))+1:ZA(T,4)=INT(RND(1)*(LV+Z1))+1
91 IFINT(RND(1)*8)+1=8THENZA(T,6)=80:ZB=$09:GOTO93
92 ZA(T,6)=81:ZB=$0F
93 IFINT(RND(1)*8)+1=4THENZA(T,5)=66:GOTO104
94 X=INT(RND(1)*8)+1
95 ONXGOTO96,97,98,99,100,101,102,103
96 ZA(T,5)=70:GOTO104
97 ZA(T,5)=71:GOTO104
98 ZA(T,5)=72:GOTO104
99 ZA(T,5)=73:GOTO104
100 ZA(T,5)=74:GOTO104
101 ZA(T,5)=77:GOTO104
102 ZA(T,5)=78:GOTO104
103 ZA(T,5)=79:GOTO104
104 VPOKE0,(SC+ZA(T,0)+(ZA(T,1)*128))*2,ZA(T,6)
105 VPOKE0,((SC+ZA(T,0)+(ZA(T,1)*128))*2)+1,ZB:NEXTT


106 REM "ITEM GENERATOR"
107 FORT=0TO2
108 IT=INT(RND(1)*7)+1
109 IFIT=1THENI=70:ZB=$0D
110 IFIT=2THENI=71:ZB=$0B
111 IFIT=3THENI=72:ZB=$0A
112 IFIT=4THENI=73:ZB=$0A
113 IFIT=5THENI=74:ZB=$0E
114 IFIT=6THENI=77:ZB=$0D
115 IFIT=7THENI=79:ZB=$0C
116 X=INT(RND(1)*18)+0:Y=INT(RND(1)*17)+0
117 IFVPEEK(0,(SC+X+(Y*128))*2)>66THEN119
118 VPOKE0,(SC+X+(Y*128))*2,I:VPOKE0,((SC+X+(Y*128))*2)+1,ZB
119 NEXTT
120FORT=1TO3
121 X=INT(RND(1)*18)+0:Y=INT(RND(1)*17)+0
122 IFVPEEK(0,(SC+X+(Y*128))*2)>66THEN121
123 IFT=1THENI=69:ZB=$0A
124 IFT=2THENI=75:ZB=$0B
125 IFT=3THENI=64:ZB=$01:CX=X:CY=Y
126 VPOKE0,(SC+X+(Y*128))*2,I:VPOKE0,((SC+X+(Y*128))*2)+1,ZB:NEXTT
127 IFL>(LM/4)*3THENFORT=0TO15:VPOKE$F,$1010+T,VPEEK($F,$1040+T):NEXTT

128 PRINT"{home}{ORANGE}{down}{right}                     "
129 TI$=T$:BR=0

130REM "TIMER"
131 PRINT"{home}{white}{down}{right}"LEFT$(TI$,2)":"MID$(TI$,3,2)":"RIGHT$(TI$,2)
132 IFL>(LM/4)*2ANDINT(RND(1)*35)+0=0THEN134
133 GOTO139
134 FOR T=0TO3:VPOKE$F,$1001,255:VPOKE$F,$1000,255:TZ=TI+2
135 IFTI<TZTHEN135
136 VPOKE$F,$1000,0:VPOKE$F,$1001,0:TZ=TI+1
137 IFTI<TZTHEN137
138 NEXTT
139 IFG1=1ANDGG=0THENFORT=0TO15:VPOKE$F,$1010+T,VPEEK($F,$1040+T):NEXTT:G1=0
140IFG1=1THENGG=GG-1:VPOKE0,(21+(13*128))*2,131+INT(GG/5)

141 REM "CHARACTER CONTOROLS"

142 TZ=TI+3
143 IFTI<TZTHEN143
144 GOSUB161
145 IFSL=1ANDGG>0THENIFL>(LM/4)*3THEN234
146 IFBB=1ANDBT>0THENGOTO172
147 IFAB=1THEN262
148 CC=(SC+CX+(CY*128))*2
149 CP=(SC+(CX+TX)+((CY+TY)*128))*2:IFVPEEK(0,CP)<65THEN131
150 IFCC=CPTHENGOTO131
151 ONVPEEK(0,CP)-65GOTO155,131,131,220,237,244,252,255,257,250
152 ONVPEEK(0,cp)-76GOTO214,217,247
153 GOTO131



154 REM"CHARACHTER MOVEMENT"
155VPOKE0,(SC+(CX+TX)+((CY+TY)*128))*2+1,1
156 VPOKE0,(SC+(CX+TX)+((CY+TY)*128))*2,64
157 VPOKE0,(SC+CX+(CY*128))*2+1,0
158 VPOKE0,(SC+CX+(CY*128))*2,66
159 CX=CX+TX:CY=CY+TY:GOTO131






160 REM "JS INPUT"
161 JS=JOY(1):TX=0:TY=0:AB=0:BB=0:SR=0:SL=0
162 IFJSAND1THENTX=1
163 IFJSAND2THENTX=-1
164 IFJSAND4THENTY=1
165 IFJSAND8THENTY=-1
166 IFJSAND16THENSR=1
167 IFJSAND32THENSL=1
168 IFJSAND64THENBB=1
169 IFJSAND128THENAB=1
170 RETURN

171REM "GUN FIRE"
172IFTX=0ANDTY=0THEN131
173 BT=BT-1: VPOKE0,(21+(9*128))*2,VPEEK(0,(21+(9*128))*2)-1
174 BX=CX+TX:BY=CY+TY:GOTO176
175 BX=BX+TX:BY=BY+TY
176 IFVPEEK(0,(SC+BX+(BY*128))*2)=66THEN179
177 IFVPEEK(0,(SC+BX+(BY*128))*2)=80ORVPEEK(0,(SC+BX+(BY*128))*2)=81THEN183
178 GOTO204
179 VPOKE0,(SC+BX+(BY*128))*2,71:VPOKE0,((SC+BX+(BY*128))*2)+1,3
180 IFVPEEK(0,(SC+(BX-TX)+((BY-TY)*128))*2)=64THEN175
181 VPOKE0,((SC+(BX-TX)+((BY-TY)*128))*2),66
182 VPOKE0,((SC+(BX-TX)+((BY-TY)*128))*2)+1,0:GOTO175
183 IFVPEEK(0,(SC+(BX-TX)+((BY-TY)*128))*2)=64THEN186
184 VPOKE0,((SC+(BX-TX)+((BY-TY)*128))*2),66
185 VPOKE0,((SC+(BX-TX)+((BY-TY)*128))*2)+1,0
186 FORT=.TO4:VPOKE0,((SC+BX+(BY*128))*2)+1,1:TZ=TI+2
187 IFTI<TZTHEN187
188 VPOKE0,((SC+BX+(BY*128))*2)+1,1:TZ=TI+1
189 IFTI<TZTHEN189
190 VPOKE0,((SC+BX+(BY*128))*2)+1,0:NEXTT
191 VPOKE0,((SC+BX+(BY*128))*2),66
192 FORT=0TO25
193 IFZA(T,0)=BXANDZA(T,1)=BYTHEN195
194 GOTO203
195 VPOKE0,((SC+BX+(BY*128))*2)+1,1
196 IFZA(T,5)=70ORZA(T,5)=77THENVPOKE0,((SC+BX+(BY*128))*2)+1,$0D
197 IFZA(T,5)=71ORZA(T,5)=75THENVPOKE0,((SC+BX+(BY*128))*2)+1,$0B
198 IFZA(T,5)=73ORZA(T,5)=72THENVPOKE0,((SC+BX+(BY*128))*2)+1,$0A
199 IFZA(T,5)=74THENVPOKE0,((SC+BX+(BY*128))*2)+1,$0E
200 IFZA(T,5)=78THENVPOKE0,((SC+BX+(BY*128))*2)+1,$09
201 IFZA(T,5)=79THENVPOKE0,((SC+BX+(BY*128))*2)+1,$0C
202 VPOKE0,((SC+BX+(BY*128))*2),ZA(T,5)
203 NEXTT:GOTO131
204 IFVPEEK(0,(SC+BX-TX+((BY-TY)*128))*2)=64THEN207
205 VPOKE0,((SC+(BX-TX)+((BY-TY)*128))*2),66
206 VPOKE0,((SC+(BX-TX)+((BY-TY)*128))*2)+1,0
207 FORT=.TO5:X=VPEEK(0,((SC+BX+(BY*128))*2)+1)
208 VPOKE0,((SC+BX+(BY*128))*2)+1,1:TZ=TI+2
209 IFTI<TZTHEN209
210 VPOKE0,((SC+BX+(BY*128))*2)+1,X:TZ=TI+1
211 IFTI<TZTHEN211
212 NEXTT:GOTO131

213REM"virus collision"
214 IFVR=99THEN131
215 VR=VR+1:VPOKE0,(24+(9*128))*2,131+VR:GOTO155

216REM"strength+ collision"
217 IFPW=20THEN131
218 PW=PW+1:VPOKE0,(24+(7*128))*2,131+PW:GOTO155



219REM "DOOR COLLISION"
220 IF KY=0THEN131
221 VPOKE0,(SC+(CX+TX)+((CY+TY)*128))*2+1,1
222 VPOKE0,(SC+(CX+TX)+((CY+TY)*128))*2,64
223 VPOKE0,(SC+CX+(CY*128))*2+1,0
224 VPOKE0,(SC+CX+(CY*128))*2,66
225 CX=CX+TX:CY=CY+TY



226REM"LEVEL COMPLETE"
227 PRINT "{home}{orange}{down}{right}level complete   "
228 T$=TI$:L=L+1:IFL<LM+1ANDL<21THENKY=0:GOTO67
229 IF L=21THENL=20:KY=0:GOTO67
230 PRINTCHR$(147)"you won!"
231 GOSUB161:IFSR=1THEN13
232 GOTO229

233 REM "GOGGLE CONTROLS
234 IFG1=1THENFORT=0TO15:VPOKE$F,$1010+T,VPEEK($F,$1040+T):NEXTT:G1=0:GOTO131
235 FORT=0TO15:VPOKE$F,$1010+T,VPEEK($F,$1030+T):NEXTT:G1=1:GOTO131

236 REM"MONEY COL"
237 MN=MN+1:IFVPEEK(0,(23+(15*128))*2)=131+99THEN240
238 VPOKE0,(23+(15*128))*2,VPEEK(0,(23+(15*128))*2)+1
239 GOTO155
240 VPOKE0,(22+(15*128))*2,VPEEK(0,(22+(15*128))*2)+1
241VPOKE0,(23+(15*128))*2,131
242 GOTO155


243 REM"BULLET COL"
244 IFBT=12THEN131
245 BT=BT+1:VPOKE0,(21+(9*128))*2,131+BT:GOTO155

246REM"GOGGLE COLLISION"
247IFGG<499THENGG=GG+INT(RND(1)*100)+50:IFGG>499THENGG=499
248 VPOKE0,(21+(13*128))*2,131+INT(GG/10):GOTO155


249 REM"KEY COL"
250 KY=1:VPOKE0,(24+(4*128))*2,75:VPOKE0,((24+(4*128))*2)+1,1:GOTO155

251REM"HEART COL"
252IFHP=PWTHEN131
253HP=HP+1:VPOKE0,(21+(7*128))*2,131+HP:GOTO155

254REM"FUEL COL"
255IFFF=20THEN131
256FF=FF+1:VPOKE0,(24+(11*128))*2,131+FF:GOTO155

257REM"BOMB COL"
258IFBM=6THEN131
259 IFVPEEK(0,((SC+(CX+TX)+((CY+TY)*128))*2)+1)=3THEN:BR=0
260BM=BM+1:VPOKE0,(21+(11*128))*2,131+BM:GOTO155



261 REM" BOMB DROP"
262 IFBR=1THEN274
263 IF TX=0ANDTY=0THEN148
264 IFBM=0THEN148
265 IFNOTVPEEK(0,(SC+CX+TX+((CY+TY))*128)*2)=66THEN131
266 VPOKE0,(SC+CX+TX+((CY+TY)*128))*2,74
267 VPOKE0,((SC+CX+TX+((CY+TY)*128))*2)+1,3
268 BX=CX+TX:BY=CY+TY:BR=1:AB=0
269 BM=BM-1:VPOKE0,(21+(11*128))*2,131+BM
270 TZ=TI+6
271 IFTI<TZTHEN271
272 GOTO131

273 REM"bomb explosion"
274 FORT=0TO1:FORTB=0TO1:FORTA=0TO8
275 B1=VPEEK(0,((SC+BX+BE(TA)+(BY*128))*2)+1)
276 IFTB=0THENB2=$20
277 IFTB=1THENB2=-$20:IFB1=0THENB2=0
278 VPOKE0,((SC+BX+BE(TA)+(BY*128))*2)+1,B1+B2
279 IF VPEEK(0,(SC+BX+BE(TA)+(BY*128))*2)=69THEN282
280 IF VPEEK(0,(SC+BX+BE(TA)+(BY*128))*2)<68THEN282
281 IFT=1THENVPOKE0,((SC+BX+BE(TA)+(BY*128))*2)+1,0
282 IF VPEEK(0,(SC+BX+BE(TA)+(BY*128))*2)>64THEN286
283 IFHP>0THENHP=HP-2:IFHP<0THENHP=0
284 VPOKE0,(21+(7*128))*2,131+HP
285 GOTO289
286 IF VPEEK(0,(SC+BX+BE(TA)+(BY*128))*2)=69THEN289
287 IF VPEEK(0,(SC+BX+BE(TA)+(BY*128))*2)<68THEN289
288 IFT=1THENVPOKE0,((SC+BX+BE(TA)+(BY*128))*2),66
289 NEXTTA:NEXTTB:NEXTT:BR=0
290 IFHP=0THEN292
291 GOTO131

292 REM "GAME OVER"
293 COLOR2,0:PRINTCHR$(147)"{home}{down*4}   game over!"
294 PRINT"{down*4}  press start"
295 IF JOY(1)=16THEN13:TZ=TI+2
296 IFTI<TZTHEN296
297 GOTO295

298 REM "LIGHT FX"

299 REM "FLICKER FX"
300 FOR T=0TO5:VPOKE$F,$1001,255:VPOKE$F,$1000,255:FORT2=.TO50:NEXTT2
301 VPOKE$F,$1000,0:VPOKE$F,$1001,0:FORT2=.TO50:NEXTT2:NEXTT:GOTO131

302 REM "NORMAL LIGHTING"
303FORT=0TO15:VPOKE$F,$1010+T,VPEEK($F,$1020+T):NEXTT:GOTO131

304 REM "NIGHT VISION"
305FORT=0TO15:VPOKE$F,$1010+T,VPEEK($F,$1030+T):NEXTT:GOTO131

306 REM"BLACKOUT"
307FORT=0TO15:VPOKE$F,$1010+T,VPEEK($F,$1040+T):NEXTT:GOTO131